module ch.fhnw.thga.fregelanguageserver.diagnostic.Diagnostic where

import ch.fhnw.thga.fregelanguageserver.compile.CompileGlobal (standardCompileGlobal)
import ch.fhnw.thga.fregelanguageserver.compile.CompileExecutor (compile)
import ch.fhnw.thga.fregelanguageserver.types.Position (Position)
import ch.fhnw.thga.fregelanguageserver.types.Range (Range, tokenToRange)
import Compiler.types.Global (StG, StIO, Message, Global, Severity(), tokens, getST, liftStG, liftIO)
import Compiler.types.Tokens (Token)
import frege.compiler.types.Positions (Position TokenPosition)
import Test.QuickCheck (Property, once, morallyDubiousIOProperty)
import Control.monad.State (evalStateT, execStateT, evalState)

data DiagnosticSeverity = ERROR | WARNING | INFORMATION | HINT

derive Eq DiagnosticSeverity
derive Show DiagnosticSeverity

fromCompilerSeverity :: Severity -> DiagnosticSeverity
fromCompilerSeverity Severity.ERROR   = ERROR
fromCompilerSeverity Severity.WARNING = WARNING
fromCompilerSeverity Severity.HINT    = HINT

data Diagnostic = Diagnostic {
    range    :: Range,
    severity :: DiagnosticSeverity,
    source   :: String,
    message  :: String
}

derive Eq Diagnostic
derive Show Diagnostic

tokensToRange :: [ Token ] -> Range
tokensToRange []     = Range { start = Position 1 0, end = Position 2 0 }
tokensToRange [ tk ] = tokenToRange tk
tokensToRange tks    = 
    let
        startToken = head tks
        endToken   = last tks
    in
    Range 
    { 
        start = Position startToken.line startToken.col,
        end   = Position endToken.line (endToken.col + (length endToken.value))  
    }

createRangeFromPos :: TokenPosition -> StG Range
createRangeFromPos pos = do
    gl  <- getST
    toks = tokens pos gl
    pure $ tokensToRange toks

createDiagnosticFromMessage :: Message -> StG Diagnostic
createDiagnosticFromMessage message = do
    range <- createRangeFromPos message.pos
    pure   $ Diagnostic 
        {
            range = range,
            severity = fromCompilerSeverity message.level, 
            source = "frege compiler", 
            message = message.text
        }

extractDiagnostics :: StG [ Diagnostic ]
extractDiagnostics = do
    gl         <- getST
    diagnostics = fmap createDiagnosticFromMessage gl.sub.messages
    sequence diagnostics

getDiagnostics :: Global -> [ Diagnostic ]
getDiagnostics = evalState $ extractDiagnostics

fregeLSPServerShouldMapNoCompilerMessagesToEmptyArray :: Property
fregeLSPServerShouldMapNoCompilerMessagesToEmptyArray = once $ morallyDubiousIOProperty do
    fregeCodeWithoutError = "module CorrectFregeTest where\n\n" ++ "ok = 42 + 42"
    global               <- standardCompileGlobal
    compiledGlobal       <- compile fregeCodeWithoutError global
    expected              = []
    actual                = getDiagnostics compiledGlobal
    pure                  $ expected == actual

fregeLSPServerShouldMapSingleCompilerMessageToDiagnostics :: Property
fregeLSPServerShouldMapSingleCompilerMessageToDiagnostics = once $ morallyDubiousIOProperty do
    fregeCodeWithError = "module ch.fhnw.thga.FaultyFregeTest where\n\nimport Does.not.Exist"
    global            <- standardCompileGlobal
    compiledGlobal    <- compile fregeCodeWithError global
    expected           = 
        [ 
            Diagnostic 
            { 
                range    = Range { start = Position 3 8, end = Position 3 22 }, 
                severity = ERROR, 
                source   = "frege compiler", 
                message  = "Could not import module frege.does.not.Exist\n(java.lang.ClassNotFoundException: frege.does.not.Exist)" 
            } 
        ]
    actual  = getDiagnostics compiledGlobal
    pure    $ expected == actual

fregeLSPServerShouldMapMultipleCompilerMessageToDiagnostics :: Property
fregeLSPServerShouldMapMultipleCompilerMessageToDiagnostics = once $ morallyDubiousIOProperty do
    fregeCodeWithErrors = "module ch.fhnw.thga.FaultyFregeTest where\n\nerr1 = do\n  x = 42\n\nerr2 = [ 22.0 ] ++ \"42\"\n\nerr3 = 42 + \"42\""
    global             <- standardCompileGlobal
    compiledGlobal     <- compile fregeCodeWithErrors global 
    expected            = 
        [ 
            Diagnostic 
            { 
                range    = Range { start = Position 6 20, end = Position 6 24 }, 
                severity = ERROR, 
                source   = "frege compiler", 
                message  = "type error in expression\n\"42\"\ntype is : String\nexpected: [t1]"
            },
            Diagnostic 
            { 
                range    = Range { start = Position 8 11, end = Position 8 12 }, 
                severity = ERROR, 
                source   = "frege compiler", 
                message  = "String is not an instance of Num" 
            },
            Diagnostic 
            { 
                range    = Range { start = Position 3 8, end = Position 3 10 }, 
                severity = ERROR, 
                source   = "frege compiler", 
                message  = "last statement in a monadic do block must not\nbe let decls" 
            }
        ]
    actual  = getDiagnostics compiledGlobal
    pure    $ expected == actual

posToTokens :: [ TokenPosition ] -> Global -> [ Token ]
posToTokens [] _      = []
posToTokens (p:ps) gl = tokens p gl ++ posToTokens ps gl

main :: IO ()
main = do
    let fregeCode       = "module ch.fhnw.thga.FaultyFregeTest where\n\nerr1 = do\n  x = 42\n\nerr2 = [ 22.0 ] ++ \"42\"\n\nerr3 = 42 + \"42\"\n\n"
    let trickyFregeCode = "module FaultyFregeTest where\n\nsimplyString s = s\n\nerr1 = (simplyString 42) ++ \"test\""
    global             <- standardCompileGlobal 
    gl                 <- compile trickyFregeCode global
    println             $ CharSequence.toString gl.sub.code
    for gl.sub.messages println
    let positions      = map (Message.pos) gl.sub.messages
    let toks           = posToTokens positions gl
    for toks println
